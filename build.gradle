apply plugin: 'java'
apply plugin: 'application'

applicationName = 'maze'
mainClassName = 'de.mtneug.maze_cli.Main'
version = '1.1'

sourceCompatibility = 8

repositories {
  mavenCentral()
  maven {
    url 'http://mvn.erichseifert.de/maven2'
  }
}

task createStatisticsDataDir() { task ->
  doLast {
    def dataDir = new File(project.buildDir, 'data')

    if (!dataDir.exists())
      if (!dataDir.mkdirs())
        throw new TaskExecutionException(task, new Exception('data directory could not be created'))
  }
}

task generateHeatMapStatistics(dependsOn: [classes, createStatisticsDataDir]) {
  doLast {
    for (d in [17, 50, 83]) {
      println('difficulty = ' + d)

      for (int w = 2; w <= 100; w += 2) {
        for (int h = 2; h <= 100; h += 2) {
          println('dimenstion = ' + w + ':' + h)

          javaexec {
            main = mainClassName
            classpath = sourceSets.main.runtimeClasspath
            args = [
              'multi-generate',
              '100',
              w + ':' + h,
              'm', '--secure-random', '--m-difficulty=' + d,
              'statistics', '--solver-name=tremaux', '--file=' + project.buildDir + '/data/heatmap-statistics.csv'
            ]
          }

          println()
        }
      }

      println()
    }
  }
}

task generateStatistics(dependsOn: [classes, createStatisticsDataDir]) {
  doLast {
    for (i in 0..100) {
      println('difficulty = ' + i)

      javaexec {
        main = mainClassName
        classpath = sourceSets.main.runtimeClasspath
        args = [
          'multi-generate',
          '5000',
          '100:50',
          'm', '--secure-random', '--m-difficulty=' + i,
          'statistics', '--solver-name=tremaux', '--file=' + project.buildDir + '/data/solver-statistics.csv'
        ]
      }

      println()
    }
  }
}

task analyseStatistics(type: Exec, dependsOn: [generateStatistics]) {
  executable 'Rscript'
  args project.buildDir.toString() + '/scripts/analyse_m_tremaux.R'
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.3'
  compile group: 'org.reflections', name: 'reflections', version: '0.9.9'
  compile group: 'de.erichseifert.vectorgraphics2d', name: 'VectorGraphics2D', version: '0.9.1'
}
